apply plugin: 'maven-publish'
apply plugin: 'com.mapbox.sdkRegistry'
apply from: file('../gradle/artifact-settings.gradle')

version = project.ext.versionName
group = project.ext.mapboxArtifactGroupId
afterEvaluate {
    def isSnapshot = project.ext.versionName.contains("-SNAPSHOT")
    components.findAll { it.name.endsWithIgnoreCase("Release") }.forEach { currentComponent ->
        // Append `-preview` for v11 private preview releases
        def componentRegistrySdkName = project.ext.mapboxRegistrySdkName + "-preview"
        registry {
            registryPublications {
                "${currentComponent.name}" {
                    sdkName = componentRegistrySdkName
                    dryRun = false
                    publish = true
                    snapshot = isSnapshot
                    publishMessage = "cc @mapbox/maps-android"
                    publications = [currentComponent.name]
                    // Use `maps_sdk_private_preview` feature flag for v11 public flavor release.

                    // **Note** that we want to protect v11 private flavor release with both `mobile_maps_private_beta`
                    // and `maps_sdk_private_preview` feature flags. However this is not supported by our sdk-registry
                    // infrastructure.
                    // As a result, for now, we will skip releasing v11 private flavor release.

                    // As a temporary placeholder, we will use `maps_sdk_private_preview` feature
                    // flag v11 flavor release.
                    featureFlag = "maps_sdk_private_preview"
                    excludeFromRootProject = project.ext.mapboxRegistryExcludeFromRootProject
                }
            }
        }

        tasks.register("javadocs${currentComponent.name}Jar", Jar) {
            def flavorDokkaJavadoc = tasks.findByName("${currentComponent.name}DokkaJavadoc")
            dependsOn(flavorDokkaJavadoc)
            classifier "javadoc"
            from flavorDokkaJavadoc.outputDirectory
            destinationDirectory.set(destinationDirectory.get().dir(currentComponent.name))
        }

        tasks.register("androidSources${currentComponent.name}Jar", Jar) {
            classifier "sources"
            // We always add the `main` kotlin which already includes the java in it
            from android.sourceSets.main.kotlin.srcDirs
            android.sourceSets.each {
                // then let's add the flavor specific folders (i.e. `release`) for current component
                if (it.name == currentComponent.name) {
                    from it.kotlin.srcDirs
                }
            }
            destinationDirectory.set(destinationDirectory.get().dir(currentComponent.name))
        }
        publishing {
            publications {
                // Find all the flavour releases (i.e. "privateRelease" and "publicRelease") and
                // create a publication per each of them
                // TODO: once we move to AGP 7.1+: https://developer.android.com/studio/publish-library/configure-pub-variants#kts
                "$currentComponent.name"(MavenPublication) {
                    from currentComponent
                    groupId this.group
                    artifactId project.ext.mapboxArtifactId
                    version this.version

                    artifact(tasks.findByName("androidSources${currentComponent.name}Jar"))
                    artifact(tasks.findByName("javadocs${currentComponent.name}Jar"))

                    pom.withXml {
                        final mainNode = asNode()
                        mainNode.appendNode('name', project.ext.mapboxArtifactTitle)
                        mainNode.appendNode('description', project.ext.mapboxArtifactTitle)
                        mainNode.appendNode('url', project.ext.mapboxArtifactUrl)

                        final licenseNode = mainNode.appendNode('licenses').appendNode('license')
                        licenseNode.appendNode('name', project.ext.mapboxArtifactLicenseName)
                        licenseNode.appendNode('url', project.ext.mapboxArtifactLicenseUrl)
                        licenseNode.appendNode('distribution', "repo")

                        final developerNode = mainNode.appendNode('developers').appendNode('developer')
                        developerNode.appendNode('id', project.ext.mapboxDeveloperId)
                        developerNode.appendNode('name', project.ext.mapboxDeveloperName)

                        final scmNode = mainNode.appendNode("scm")
                        scmNode.appendNode("connection", project.ext.mapboxArtifactScmUrl)
                        scmNode.appendNode("developerConnection", project.ext.mapboxArtifactScmUrl)
                        scmNode.appendNode("url", project.ext.mapboxArtifactUrl)
                    }
                }
            }
        }
    }
}

tasks.withType(Javadoc).configureEach {
    options.addStringOption('encoding', 'UTF-8')
    options.addStringOption('docencoding', 'UTF-8')
    options.addStringOption('charset', 'UTF-8')
}
